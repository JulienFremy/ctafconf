#!/bin/sh
## zshprompt for ctafconf in /ctaf/conf/ctafconf/zsh
##
## Made by GESTES Cedric
## Login   <ctaf42@gmail.com>
##
## Started on  Fri Feb 11 01:37:23 2005 GESTES Cedric
## Last update Wed Nov 25 14:14:29 2009 ctaf42
##
##CTAFCONF

#source ~/.config/ctafconf/etc/zsh/zgitinit.zsh

autoload -Uz zgitinit && zgitinit
autoload -Uz colors   && colors
autoload -Uz vcs_info
autoload -Uz add-zsh-hook

####################################
# COLORS
####################################
bright="\033[m1;"
dull="\033[m0;"
for color in BLACK RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
	#eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
  eval export PR_$color='%{$fg_no_bold[${(L)color}]%}'
  eval export PR_BG_$color='%{$bg[${(L)color}]%}'
  eval export PR_LIGHT_$color='%{$fg_bold[${(L)color}]%}'
  (( count = $count + 1 ))
done
PR_NO_COLOUR="%{$terminfo[sgr0]%}"



####################################
# VCS_INFO
####################################
#may be slow
zstyle ':vcs_info:*:ctaf:*' check-for-changes false
#may be slow
zstyle ':vcs_info:*:ctaf:*' get-revision      false
zstyle ':vcs_info:*:ctaf:*' use-simple        false
zstyle ':vcs_info:*:ctaf:*' max-exports       2

# set formats
# %b - branchname
# %u - unstagedstr (see below)
# %c - stangedstr (see below)
# %a - action (e.g. rebase-i)
# %R - repository path
# %S - path in the repository
#default format for standard vcs
ACTION="[${PR_LIGHT_RED}%a${PR_LIGHT_WHITE}]"
FORMAT="%s on ${PR_MAGENTA}%b${PR_LIGHT_WHITE}"
FORMAT_ACTION="$FORMAT $ACTION"
zstyle ':vcs_info:*:ctaf:*' formats             "$FORMAT" "%s"
zstyle ':vcs_info:*:ctaf:*' actionformats       "$FORMAT_ACTION" "%s"
zstyle ':vcs_info:*:ctaf:*' branchformat        "${PR_MAGENTA}%b${PR_LIGHT_WHITE}:${PR_LIGHT_RED}%r${PR_NO_COLOUR}"

prompt_ctaf_git_tracking_branch()
{
  t=$(zgit_tracking_merge)
  if ! [ "$t" = "" ] ; then
    echo " tracking${PR_LIGHT_MAGENTA} ${t}${PR_LIGHT_WHITE}"
  fi
}

#format vcs information, append information to vcs_info that is a little poor
#this function is called each time a prompt need to be display
#this function set the psvar with changing information
prompt_ctaf_precmd() {
  setopt noxtrace noksharrays localoptions
  #if the function is not found, fail silently (degraded mode)
  vcs_info 'ctaf' 2>/dev/null
  psvar=()
  case "$vcs_info_msg_1_" in
    git)
      GIT_FORMAT="$vcs_info_msg_0_ (${PR_LIGHT_YELLOW}$(git describe --always --tags 2>/dev/null)${PR_LIGHT_WHITE})$(prompt_ctaf_git_tracking_branch 2>/dev/null)"
      psvar[1]="[$GIT_FORMAT]"
      ;;
    git-svn)
      GITSVN_FORMAT="$vcs_info_msg_0_ (${PR_LIGHT_YELLOW}$(git describe --always --tags) ${PR_MAGENTA}$(zgit_svnbranch)${PR_LIGHT_WHITE}:${PR_LIGHT_RED}$(zgit_svnhead)${PR_LIGHT_WHITE})$(prompt_ctaf_git_tracking_branch)"
      psvar[1]="[$GITSVN_FORMAT]"
      ;;
    "")
      #no vcs info => dont do anything
      ;;
    *)
      psvar[1]="[$vcs_info_msg_0_]"
      ;;
  esac
  if [ "$psvar[1]" != "" ] ; then
    psvar[1]="${PR_BG_BLACK}${PR_LIGHT_WHITE}$psvar[1]${PR_NO_COLOUR}
"
  fi
}



prompt_ctaf_setup () {
  add-zsh-hook precmd prompt_ctaf_precmd 2>/dev/null
  BASE_PROMPT="${PR_BG_BLACK}${PR_LIGHT_MAGENTA}%(!.%SROOT%s.${PR_LIGHT_WHITE}%n${PR_LIGHT_YELLOW})\
@${PR_LIGHT_GREEN}%m${PR_LIGHT_MAGENTA} \
[%(?.${PR_LIGHT_WHITE}$?.${PR_LIGHT_RED}% err %?)${PR_LIGHT_MAGENTA}] \
[${PR_LIGHT_BLUE}%~${PR_LIGHT_MAGENTA}]${PR_NO_COLOUR}
${PR_BG_BLACK}${PR_LIGHT_WHITE}z${PR_LIGHT_GREEN}%(!.#.$)${PR_NO_COLOUR} "

  PROMPT="\$psvar[1]${BASE_PROMPT}"
  RPROMPT="[${PR_BG_BLACK}${PR_LIGHT_CYAN}%D ${PR_LIGHT_GREEN}%T${PR_NO_COLOUR}]"

  PS2=''
}


prompt_ctaf_setup "$@"
