#!/bin/tcsh
# .tcshrc - 30/06/05 19:16 - Turea Alexandru Teodor (classicmanpro@yahoo.com)

## Warning ##
#never edit this file
#please edit ~/.config/ctafconf/perso/tcshrc.mine


#######################
# ##### GENERAL ##### #
#######################

#ne rien faire si on est pas en mode interactif TODO
if ($?prompt && a$HOST != agate-ssh) then

cat ~/.config/ctafconf/VERSION
echo -n "shell: tcsh, "
cat ~/.config/ctafconf/etc/shell/motd

umask 022

limit    coredumpsize 0
limit -h coredumpsize 0


###############################
# ##### SHELL VARIABLES ##### #
###############################
set color
set filec
set rmstar
set dunique
set autolist
set ellipsis
set dextract
set addsuffix
set noclobber
set autoexpand
#set autocorrect
set pushdsilent
set pushdtohome
#set printexitvalue

set history     = 50
set savehist    = 50
set histdup     = all

set savedirs    = 50

set killring    = 50
set killdup     = all

set correct     = all
set listjobs    = long
set echo_style  = both
set symlinks    = chase
set inputmode   = insert
set matchbeep   = ambiguous
set promptchars = '$#'
set listflags   = 'A'
set nobeep
set watch       = (0 nobody nobody)
set who         = "%n(`whoami`) has %a %l from %M"

unset {histfile,dirsfile,autologout,notify,tperiod} #implicitcd,

#######################
# ##### ALIASES ##### #
#######################

alias    .                'source'

unalias {beepcmd,cwdcmd,jobcmd,periodic,precmd,postcmd,shell}

###########################
# ##### COMPLETIONS ##### #
###########################
complete {mkfs,fsck} 'n/-t/(4.2 4.3 adfs affs autofs bdev coda coherent cramfs devpts efs ext ext2 ext3 hfs hpfs iso9660 jfs mfs minix msdos ncpfs nfs ntfs pipefs proc qnx4 ramfs reiserfs romfs rootfs S51K S52K shm smbfs sockfs sysv tmp tmpfs udf ufs umsdos usbfs usbdevfs vfat xenix xfs xiafs)/'
complete {man,where,which,whatis,sudo,info,apropos} 'p/*/c/'
complete kill 'c/-/S/' 'n/-s/S/' 'c/%/j/' 'p/*/(%)/'
complete qmake 'n/-o/(Makefile)/' 'p/*/f:*.pro/'
complete {cat,pico,nano,vi,vim,view,emacs} 'p/*/f/'
complete {{,un}setenv,printenv,env} 'p/*/e/'
complete {fg,bg,stop} 'c/%/j/' 'p/*/(%)/'
complete {,un}alias 'p/*/c/' 'p/*/a/'
complete {cd,pushd,{ch,mk,rm}dir} 'p/*/d/'
complete {finger,pinky,groups} 'p/*/u/'
complete {,un}complete 'p/1/X/'
complete {,un}set 'p/*/s/'

############################
# ##### KEY BINDINGS ##### #
############################
bindkey "^[j" complete-word-back
bindkey "^[k" complete-word-fwd

bindkey "^[m" list-glob

bindkey "^[e" expand-line
bindkey "^[g" expand-glob
bindkey "^[v" expand-variables

bindkey "^[z" normalize-path
bindkey "^[x" normalize-command

bindkey "^[i" i-search-fwd
bindkey "^[I" i-search-back

bindkey "^[a" spell-line
bindkey "^[o" overwrite-mode
bindkey "^[q" keyboard-quit

if ( -f ~/.config/ctafconf/etc/tcsh/tcshprompt ) source ~/.config/ctafconf/etc/tcsh/tcshprompt

if ( -f ~/.config/ctafconf/etc/shell/env ) source ~/.config/ctafconf/etc/shell/env csh

ls ~/.config/ctafconf/perso/env | null_cmd grep '.env$'
if ( ! $? ) then
  foreach f ( ~/.config/ctafconf/perso/env/*.env )
    source $f;
  end
endif


if ( -f ~/.config/ctafconf/etc/shell/alias ) source ~/.config/ctafconf/etc/shell/alias

if ( -f ~/.config/ctafconf/perso/tcshrc.mine ) source ~/.config/ctafconf/perso/tcshrc.mine

endif
